#!/usr/bin/osascript
# Checks if any member of strList starts with str
on starts_with(str, strList)
  repeat with cur in strList
    if str starts with cur then return true
  end repeat
  return false
end starts_with


tell application "TaskPaper" to reopen activate

tell application "System Events"
  set docTitles to {"Main", "Work"}

  set resolutions to {}
  repeat with p in paragraphs of (do shell script "system_profiler SPDisplaysDataType | awk '/Resolution:/{ printf \"%s %s\\n\", $2, $4 }'")
    set resolutions to resolutions & {{word 1 of p as number, word 2 of p as number}}
  end repeat

  set {DW, DH} to first item of resolutions
  set WW to (DW / 1.5)
  set WH to (DH / 1.5)
  set WX to (DW / 6)
  set WY to 26


  tell process "TaskPaper"
    repeat with wIdx from count(windows) to 1 by -1
      set wName to (name of item wIdx of windows)
      if my starts_with(wName, docTitles) then
        tell item wIdx of windows
          set {size, position} to {{WW, WH}, {WX, WY}}
          perform action "AXRaise"
        end tell
        return 0
      end if
    end repeat
  end tell
end tell
